plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.devtools.ksp'
}

android {
    namespace 'com.talkar.app'
    compileSdk 34

    defaultConfig {
        applicationId "com.talkar.app"
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // NOTE: API_* BuildConfig fields are set per-buildType below.
        // They are intentionally omitted here to force explicit per-variant values.
        
        // Support for 16KB page size (required for Android 15+ from Nov 2025)
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }
    }
    
    packaging {
        jniLibs {
            // Enable 16KB page size alignment for native libraries
            useLegacyPackaging = false
        }
    }

    // Allow overriding API host/protocol via project properties (e.g., -PAPI_HOST=... or in gradle.properties)
    def apiProtocolDebug = project.hasProperty('API_PROTOCOL') ? project.API_PROTOCOL : 'https'
    def apiHostDebug = project.hasProperty('API_HOST') ? project.API_HOST : '10.0.2.2'
    // Ports should be integers for buildConfigField "int"
    def apiPortDebug = project.hasProperty('API_PORT') ? project.API_PORT.toInteger() : 443
    def apiVersion = project.hasProperty('API_VERSION') ? project.API_VERSION : 'v1'

    // Release (production) defaults should use HTTPS and a production host. CI or local overrides can set API_HOST_RELEASE etc.
    def apiProtocolRelease = project.hasProperty('API_PROTOCOL_RELEASE') ? project.API_PROTOCOL_RELEASE : 'https'
    def apiHostRelease = project.hasProperty('API_HOST_RELEASE') ? project.API_HOST_RELEASE : 'talkar-backend.onrender.com'
    def apiPortRelease = project.hasProperty('API_PORT_RELEASE') ? project.API_PORT_RELEASE.toInteger() : 443

    buildTypes {
        debug {
            // Debug uses local/dev defaults but can be overridden via -P or gradle.properties
            buildConfigField "String", "API_PROTOCOL", "\"${apiProtocolDebug}\""
            buildConfigField "String", "API_HOST", "\"${apiHostDebug}\""
            buildConfigField "int", "API_PORT", "${apiPortDebug}"
            buildConfigField "String", "API_VERSION", "\"${apiVersion}\""
            buildConfigField "boolean", "IS_BETA", "false"
        }

        beta {
            // Beta build for internal testing
            applicationIdSuffix ".beta"
            versionNameSuffix "-beta"
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            
            // Beta uses production backend
            buildConfigField "String", "API_PROTOCOL", "\"${apiProtocolRelease}\""
            buildConfigField "String", "API_HOST", "\"${apiHostRelease}\""
            buildConfigField "int", "API_PORT", "${apiPortRelease}"
            buildConfigField "String", "API_VERSION", "\"${apiVersion}\""
            buildConfigField "boolean", "IS_BETA", "true"
        }

        release {
            // Production defaults use HTTPS and production host; allow CI overrides
            buildConfigField "String", "API_PROTOCOL", "\"${apiProtocolRelease}\""
            buildConfigField "String", "API_HOST", "\"${apiHostRelease}\""
            buildConfigField "int", "API_PORT", "${apiPortRelease}"
            buildConfigField "String", "API_VERSION", "\"${apiVersion}\""
            buildConfigField "boolean", "IS_BETA", "false"

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    
    kotlinOptions {
        jvmTarget = '1.8'
    }
    
    buildFeatures {
        compose true
        buildConfig true
    }
    
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.3'
    }
    
    lint {
        disable 'UnsafeOptInUsageError'
    }
    
    // Exclude archive folders from compilation
    sourceSets {
        main {
            java {
                exclude '**/archive/**'
            }
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.7.0'
    implementation 'androidx.activity:activity-compose:1.8.2'
    implementation platform('androidx.compose:compose-bom:2023.10.01')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.material:material-icons-core:1.5.4'
    implementation 'androidx.compose.material:material-icons-extended:1.5.4'
    
    // ARCore with Sceneview for 3D rendering
    implementation 'com.google.ar:core:1.43.0'
    implementation 'io.github.sceneview:arsceneview:2.2.1'
    
    // Sceneform removed due to Filament version conflict with Sceneview
    // Using custom Filament-based video renderer instead
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.7.0'

    // CameraX
    def camerax_version = "1.3.1"
    implementation "androidx.camera:camera-core:${camerax_version}"
    implementation "androidx.camera:camera-camera2:${camerax_version}"
    implementation "androidx.camera:camera-lifecycle:${camerax_version}"
    implementation "androidx.camera:camera-view:${camerax_version}"
    
    // Networking
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    
    // Image loading
    implementation 'io.coil-kt:coil-compose:2.5.0'
    
    // Permissions
    implementation 'com.google.accompanist:accompanist-permissions:0.32.0'
    
    // ViewModel
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.7.0'
    
    // Navigation
    implementation 'androidx.navigation:navigation-compose:2.7.6'
    
    // Room database
    implementation 'androidx.room:room-runtime:2.6.1'
    implementation 'androidx.room:room-ktx:2.6.1'
    ksp 'androidx.room:room-compiler:2.6.1'
    
    // Gson for TypeConverters
    implementation 'com.google.code.gson:gson:2.10.1'
    
    // ML Kit - REMOVED (using ARCore Augmented Images instead)
    // implementation 'com.google.mlkit:face-detection:16.1.7'
    // implementation 'com.google.android.gms:play-services-mlkit-face-detection:17.1.0'
    // implementation 'com.google.mlkit:image-labeling:17.0.7'
    // implementation 'com.google.mlkit:object-detection:17.0.1'
    // implementation 'com.google.mlkit:vision-common:17.3.0'
    // implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.7.3'
    
    // ExoPlayer for video playback
    implementation 'androidx.media3:media3-exoplayer:1.2.1'
    implementation 'androidx.media3:media3-ui:1.2.1' // For PlayerView
    implementation 'androidx.media3:media3-ui:1.2.1'
    
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.7.0'
    testImplementation 'org.robolectric:robolectric:4.11.1'
    testImplementation 'androidx.arch.core:core-testing:2.2.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.3'
    testImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation platform('androidx.compose:compose-bom:2023.10.01')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
}